plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.0.9'

    id 'biz.aQute.bnd.builder' version '5.2.0'
    id 'com.vanniktech.maven.publish' version '0.13.0'
}

group 'io.github.palexdev'
version '0.7.0'

repositories {
    mavenCentral()
    jcenter()
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

dependencies {
    testImplementation "org.testfx:testfx-core:4.0.16-alpha"
    testImplementation "junit:junit:4.13.1"
    testImplementation "org.testfx:testfx-junit:4.0.16-alpha"

    implementation 'org.reactfx:reactfx:2.0-M5'
}

javafx {
    version = "15.0.1"
    modules = [ 'javafx.controls' ]
}

javadoc {
    excludes = ['**/*.html', 'META-INF/**']

    options.use = true
    options.splitIndex = true
    options.encoding = 'UTF-8'
    options.author = true
    options.version = true
    options.windowTitle = "$project.name $project.version API"
    options.docTitle = "$project.name $project.version API"
    options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api',
                     'https://openjfx.io/javadoc/15']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJarBuild(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJarBuild
    archives jar
}

jar {
    manifest {
        attributes(
                'Bundle-Name': project.name,
                'Bundle-Description': "An updated version of Flowless",
                'Bundle-SymbolicName': 'io.github.palexdev',
                'Export-Package': 'io.github.palexdev.flowless.*'
        )
    }
}

task removeBnd(type: Delete) {
    delete fileTree(project.buildDir) {
        include '**/*.bnd'
    }
}

build.dependsOn removeBnd
